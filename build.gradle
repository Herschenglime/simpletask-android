buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

apply plugin: 'com.android.application'

// Generate some build info. Used for the revision in the application settings and for generating the deployed file names.
 
task buildInfo {
    def revcmd = "git rev-parse --short HEAD"
    def revproc = revcmd.execute()
    ext.revision = revproc.text.trim()
    def datecmd = "git log -1 --date=short --pretty=format:%cd"
    def dateproc = datecmd.execute()
    ext.date = dateproc.text.trim()
    def branchcmd = 'git rev-parse --abbrev-ref HEAD'
    def branchproc = branchcmd.execute()
    ext.branch = branchproc.text.trim()
}

// Check if all changes are commited to Git. If not throw an exception.
// This is called before deploying apk's. It will ensure that the Git revision numbers in the apk names
// and in the app settings are accurate. If there were uncommited changes, the result would be different from
// a clean checkout.

task checkCleanGit {
    def cmd = "git diff-files"
    def proc = cmd.execute()
    ext.status = proc.text.trim()
    doLast {
       if (!(ext.status == ""))
          throw new GradleException("Git not clean, commit changes before running serve:\n" + ext.status)
    }
}

def checkDeployPath() {
    if (hasProperty("deployPath"))
        return deployPath
    else
        return null
}

// copy generated apks to path. Include can't use wildcards because the are determined at configuration time.
task deploy(type:Copy , dependsOn: ['checkCleanGit']) {
  doFirst {
    if (checkDeployPath()==null)
      throw new GradleException("deployPath not defined, please define it in gradle.properties")
  }
  into checkDeployPath() + '/' + buildInfo.ext.branch + '/' + buildInfo.ext.date + "-" + buildInfo.ext.revision
  from 'build/outputs/apk/'
  include 'simpletask-android-cloudless-release.apk'
  include 'simpletask-android-free-release.apk'
}

// Call gradle with -Plint to enable all warnings.
if (hasProperty("lint")) { 
   tasks.withType(Compile) { 
      options.compilerArgs << "-Xlint:all" 
   } 
}


android {


    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    buildToolsVersion "22.0.1"
    compileSdkVersion 22

    defaultConfig {
        versionName '5.3.0'
        versionCode 1547
	    minSdkVersion 14
        targetSdkVersion 22
        buildConfigField "String", "GIT_REV", "\"" + buildInfo.ext.revision + "\""
    }

    productFlavors {
        free {
            applicationId 'nl.mpcjanssen.todotxtholo'
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
        }
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    freeCompile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.intellij:annotations:12.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.github.rjeschke:txtmark:0.11'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'com.google.guava:guava:18.0'
    compile 'org.luaj:luaj-jse:3.0'
    testCompile 'junit:junit:4.12'
}

deploy.mustRunAfter clean
preBuild.dependsOn buildInfo

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
