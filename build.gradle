buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.6'
    }
}

apply plugin: 'android'

android {
    buildToolsVersion "18.0.1"
    compileSdkVersion 18

    defaultConfig {
        versionName '3.0.0'
        versionCode 1037
    }

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.android.support:support-v4:18.0.0'
    }

    productFlavors {
        free {
            packageName 'nl.mpcjanssen.todotxtholo'
            buildConfig "public final static boolean CLOUDLESS = false;"
            versionName android.defaultConfig.versionName + ' (' + getFossilUUID() +')'
        }
        donate {
            packageName 'nl.mpcjanssen.simpletask.donate'
            buildConfig "public final static boolean CLOUDLESS = false;"
            versionName defaultConfig.versionName + '-donate (' + getFossilUUID() +')'
        }
        dev {
            packageName 'nl.mpcjanssen.simpletask.dev'
            buildConfig "public final static boolean CLOUDLESS = false;"
            versionName defaultConfig.versionName + '-dev (' + getFossilUUID() +')'
        }
        // Special flavor for testing as this is uninstalled after
        // completion of the test run
        unit {
            packageName 'nl.mpcjanssen.simpletask.test'
            buildConfig "public final static boolean CLOUDLESS = true;"
        }
        cloudless {
            packageName 'nl.mpcjanssen.simpletask'
            buildConfig "public final static boolean CLOUDLESS = true;"
            versionName defaultConfig.versionName + '-cloudless (' + getFossilUUID() +')'
        }
        cloudlessdonate {
            packageName 'nl.mpcjanssen.simpletaskdonate'
            buildConfig "public final static boolean CLOUDLESS = true;"
            versionName defaultConfig.versionName + '-cloudless-donate (' + getFossilUUID() +')'
        }
    }
    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    check.dependsOn 'connectedInstrumentTestUnitDebug'
}

// Only sign if we have keystore properties in the gradle.properties
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

def getFossilUUID() {
    String contents = file( 'manifest.uuid' ).getText( 'UTF-8' )
    return contents.substring(0,8);
}
