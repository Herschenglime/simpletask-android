buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

apply plugin: 'android'

android {
    buildToolsVersion "19.0.0"
    compileSdkVersion 19

    defaultConfig {
        versionName '4.0.6'
        versionCode 1061
    }

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.android.support:support-v4:19.0.0'
    }

    productFlavors {
        free {
            packageName 'nl.mpcjanssen.todotxtholo'
            buildConfig "public final static boolean CLOUDLESS = false;"
        }
        donate {
            packageName 'nl.mpcjanssen.simpletask.donate'
            buildConfig "public final static boolean CLOUDLESS = false;"
        }
        // Special flavor for testing as this is uninstalled after
        // completion of the test run
        unit {
            packageName 'nl.mpcjanssen.simpletask.test'
            buildConfig "public final static boolean CLOUDLESS = true;"
        }
        cloudless {
            packageName 'nl.mpcjanssen.simpletask'
            buildConfig "public final static boolean CLOUDLESS = true;"
        }
        cloudlessdonate {
            packageName 'nl.mpcjanssen.simpletaskdonate'
            buildConfig "public final static boolean CLOUDLESS = true;"
        }
    }
    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    check.dependsOn 'connectedInstrumentTestUnitDebug'
}

// Only sign if we have keystore properties in the gradle.properties
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
