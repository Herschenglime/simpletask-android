buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.2'
    }
}

apply plugin: 'android'

android {
    buildToolsVersion "17.0"
    compileSdkVersion 18

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.android.support:support-v4:13.0.0'
    }

    productFlavors {
        free {
            packageName 'nl.mpcjanssen.todotxtholo'
        }
        donate {
            packageName 'nl.mpcjanssen.simpletask.donate'
        }
        dev {
            packageName 'nl.mpcjanssen.simpletask.dev'
        }
        // Special flavor for testing as this is uninstalled after
        // completion of the test run
        unit {
            packageName 'nl.mpcjanssen.simpletask.test'
        }
    }
    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    check.dependsOn 'connectedInstrumentTestUnitDebug'
}

// Only sign if we have keystore properties in the gradle.properties
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}


