buildscript {

    ext.kotlin_version = '1.0.0-beta-4584'
    ext.android_support_version = '23.1.1'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
	classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    buildToolsVersion "23.0.2"
    compileSdkVersion 23

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }


    defaultConfig {
        versionName '6.3.4'
        versionCode 3006
	    minSdkVersion 14
        targetSdkVersion 23
	dexOptions {
        	incremental true
	}

    }

    productFlavors {
        dropbox {
            applicationId 'nl.mpcjanssen.todotxtholo'
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
        }
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    dropboxCompile fileTree(dir: 'libs', include: '*.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile 'com.github.rjeschke:txtmark:0.11'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'org.luaj:luaj-jse:3.0.1'
    testCompile 'junit:junit:4.12'
}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
