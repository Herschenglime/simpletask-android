buildscript {

    ext.kotlin_version = '1.0.0-beta-4583'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
	classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Call gradle with -Plint to enable all warnings.
if (hasProperty("lint")) {
    //noinspection GroovyAssignabilityCheck
    tasks.withType(Compile) {
      options.compilerArgs << "-Xlint:all" 
   } 
}

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    buildToolsVersion "23.0.2"
    compileSdkVersion 23

    defaultConfig {
        versionName '6.2.0'
        versionCode 3001
	minSdkVersion 14
        targetSdkVersion 23
    }

    productFlavors {
        dropbox {
            applicationId 'nl.mpcjanssen.todotxtholo'
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
        }
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    dropboxCompile fileTree(dir: 'libs', include: '*.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.github.rjeschke:txtmark:0.11'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'org.luaj:luaj-jse:3.0'
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'
    compile 'org.slf4j:slf4j-api:1.7.6'
    testCompile 'junit:junit:4.12'
}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
